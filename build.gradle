plugins {
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
	alias(libs.plugins.quilt.loom)
}

repositories {
	mavenLocal()
	maven { url = "https://maven.shedaniel.me/" }
	maven { url = "https://maven.siphalor.de" }
	maven { url = "https://maven.terraformersmc.com/" }
	maven { url = "https://maven.quiltmc.org/repository/" }
	maven { url = "https://maven.parchmentmc.org" }
	maven { url = "https://jitpack.io" }
	maven { url = "https://mvn.devos.one/snapshots/" }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

archivesBaseName = project.archives_base_name
version = "${libs.versions.version.get()}+build.${libs.versions.build.get()}-${libs.versions.minecraft.get()}"
group = project.maven_group

dependencies {
	minecraft libs.minecraft
	mappings loom.layered {
		it.addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:v2"))
		it.parchment("org.parchmentmc.data:parchment-1.18.2:2022.07.10@zip")
		it.officialMojangMappings {
			setNameSyntheticMembers(false)
		}
	}

	modImplementation libs.quilt.loader

	modImplementation (libs.bundles.qsl) {
		force = true
	}

	modImplementation(libs.vampirelib)
	include(libs.vampirelib)
}

processResources {
	inputs.property "version", version

	filesMatching('fabric.mod.json') {
		expand "version": version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
	withJavadocJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		if (project.hasProperty("siphalorMavenUser")) {
			maven {
				name = "Siphalor"
				url = "https://maven.siphalor.de/upload.php"
				credentials {
					username = siphalorMavenUser
					password = siphalorMavenPassword
				}
			}
		}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "C7pWNmLQ"
	versionNumber = project.version
	versionType = "beta"
	uploadFile = remapJar
	gameVersions = [libs.versions.minecraft.get()]
	loaders = ["quilt"]
	dependencies {
		required.project "P7dR8mSH"
	}
}