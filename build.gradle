plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'maven-publish'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id "io.github.juuxel.loom-quiltflower" version "1.6.0"
	id 'org.quiltmc.quilt-mappings-on-loom' version '4.0.0'
}

repositories {
	mavenLocal()
	maven { url = "https://maven.shedaniel.me/" }
	maven { url = "https://raw.githubusercontent.com/vampire-studios/VampireStudiosMaven/main/" }
	maven { url = "https://maven.terraformersmc.com/" }
	maven { url = "https://maven.quiltmc.org/repository/" }
	maven { url = "https://maven.parchmentmc.org" }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered {
		it.addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:1.18.2+build.2:v2")) // https://maven.quiltmc.org/#browse/browse:release
		it.parchment("org.parchmentmc.data:parchment-1.18.1:2022.02.13@zip") // https://ldtteam.jfrog.io/ui/natiave/parchmentmc-public/org/parchmentmc/data/parchment-1.18.1/BLEEDING-SNAPSHOT
		it.officialMojangMappings {
			setNameSyntheticMembers(false)
		}
	}

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
		force = true
	}

	modApi("me.shedaniel.cloth:cloth-config-fabric:6.2.57") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	include("me.shedaniel.cloth:cloth-config-fabric:6.2.57") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	modImplementation ("io.github.vampirestudios:VampireLib-Fabric:${project.vampirelib_version}") {
		exclude group: "net.fabricmc.fabric-api"
		exclude group: "io.github.cottonmc"
	}
	include ("io.github.vampirestudios:VampireLib-Fabric:${project.vampirelib_version}") {
		exclude group: "net.fabricmc.fabric-api"
		exclude group: "io.github.cottonmc"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	withSourcesJar()
	withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			url file("E:\\Development\\Work\\VampireStudiosMaven\\")
		}
	}
}

curseforge {
	apiKey = System.getenv('CURSEKEY') ?: "a key"
	project {
		id = "341464"
		releaseType = "release"
		changelog = project.changelog
		gameVersionStrings.clear()
		gameVersionStrings.add("1.18.1")
		gameVersionStrings.add("Fabric")
		mainArtifact(file("build/libs/${archivesBaseName}-${version}.jar")) {
			displayName = "[" + project.minecraft_version + "] RAA Core " + project.mod_version
			relations {
				requiredDependency "fabric-api"
				embeddedLibrary "vampirelib"
				embeddedLibrary "cloth-config"
				embeddedLibrary "auto-config-updated-api"
				optionalDependency "modupdater"
			}
		}
	}
	options{
		forgeGradleIntegration = false
	}
}